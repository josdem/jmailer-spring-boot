buildscript {
  ext {
    springBootVersion = '1.3.0.RELEASE'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
  }
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

plugins {
  id 'com.craigburke.bower-installer' version '2.5.1'
}

bower {
  installBase = 'web/src/main/resources/static/assets/third-party'
  'bootstrap'('3.3.6'){
    source '**'
  }
}

ext {
  springfoxVersion = '2.4.0'
}

subprojects {
  apply plugin: 'groovy'
  apply plugin: 'spring-boot'

  repositories {
    mavenCentral()
  }

  dependencies {
    compile 'org.codehaus.groovy:groovy:2.4.5'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4'
  }

  bootRun {
    systemProperties = System.properties
  }

  test {
    systemProperties = System.properties
  }
}

project(":services"){
  dependencies {
    compile 'org.springframework.boot:spring-boot-starter-aop'
    compile 'org.springframework.boot:spring-boot-starter-mail'
    compile 'org.springframework:spring-jms'
    compile 'org.apache.activemq:activemq-broker'
    compile 'org.freemarker:freemarker:2.3.23'
    compile "io.springfox:springfox-swagger2:${springfoxVersion}"
    compile "io.springfox:springfox-swagger-ui:${springfoxVersion}"
  }
}

project(":web"){
  apply plugin:"war"
  war.archiveName "jmailer.war"

  dependencies{
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile project(":services")
  }

  bootRun.dependsOn rootProject.tasks['bowerInstall']
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.7'
}

